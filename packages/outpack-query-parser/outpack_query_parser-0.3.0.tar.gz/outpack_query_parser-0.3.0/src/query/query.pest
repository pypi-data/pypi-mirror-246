// See the pest editor https://pest.rs/#editor for an easy way to see how
// inputs are parsed by this grammar

query = { SOI ~ toplevel ~ EOI }
toplevel = _{ body | shortformLatest | shortformId }
body  = { expr ~ (booleanOperator ~ expr)* }
shortformLatest = { "latest" }
shortformId = { string }

prefix   = _{ negation }
negation = { "!" }

booleanOperator = _{ and | or }
and             = { "&&" }
or              = { "||" }

expr = _{ prefix? ~ (brackets | singleVariableFunc | noVariableFunc | infixExpression) }

brackets = { "(" ~ body ~ ")" }

noVariableFunc          =  { noVariableFuncNames ~ "()" }
singleVariableFunc      =  { singleVariableFuncNames ~ "(" ~ body ~ ")" }
noVariableFuncNames     =  _{ latest }
singleVariableFuncNames =  _{ latest | single }
latest                  =  { "latest" }
single                  =  { "single" }

infixExpression = { testValue ~ infixFunction ~ testValue }
infixFunction   = @{ ("=" | "!" | "<" | ">"){1,2} }

testValue         = _{ lookup | literal}
lookup            = { lookupPacket | lookupThis | lookupEnvironment  }
lookupPacket      = { lookupPacketId | lookupPacketName | lookupPacketParam }
lookupPacketId    = { "id" }
lookupPacketName  = { "name" }
lookupPacketParam = { "parameter:" ~ identifier }
lookupThis        = { "this:" ~ identifier }
lookupEnvironment = { "environment:" ~ identifier }
literal           = { string | boolean | number }

identifier = @{ (ASCII_ALPHANUMERIC | "_" )+ }

number = @{
    ("-" | "+")?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

boolean    = { "true" | "TRUE" | "True" | "false" | "FALSE" | "False" }
string     = ${ ("\"" ~ inner_dbl ~ "\"") | ("'" ~ inner_sgl ~ "'") }
// Contents of string including double quotes
inner_dbl  = @{ char_dbl* }
inner_sgl  = @{ char_sgl* }
// Contents of string without double quotes
char_dbl   =  {
    !("\"" | "\\") ~ ANY // Matches any character except " and \ which are not supported in querying atm
}
char_sgl   =  {
    !("'" | "\\") ~ ANY // Matches any character except ' and \ which are not supported in querying atm
}
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
